\begin{vdm_al}
module GuiDB

 imports 

   from MapDB
     types
       key = int renamed key;
       data = int renamed data;
       mapdb = map MapDB`key to MapDB`data renamed mapdb
     
     functions
       Insert : MapDB`key * MapDB`data * MapDB`mapdb -> MapDB`mapdb
       renamed Insert;
       LookUp : MapDB`key * MapDB`mapdb -> MapDB`data renamed LookUp;
       Defined : MapDB`key * MapDB`mapdb -> bool renamed Defined;
       Remove : MapDB`key * MapDB`mapdb -> MapDB`mapdb renamed Remove,

   from GUI
    functions
      GetData : () -> MapDB`key * MapDB`data renamed GetData;
      GetKey : () -> MapDB`key renamed GetKey

    operations
      ShowData : MapDB`key * MapDB`data  ==> () renamed ShowData;
      ShowMsg : nat ==> () renamed ShowMsg;
      ShowDefined : MapDB`key * bool ==> () renamed ShowDefined;
      SelectFunc : () ==> int renamed SelectFunc;
      GUI_Init : () ==> () renamed GUI_Init

  exports all
  
definitions

state database of 
  db : mapdb
  init initdb == initdb = mk_database({|->})
end

operations 
-- Grafical User Interface

  GUI_Insert : () ==> ()
  GUI_Insert() ==
    let mk_(k,d) = GetData() in
    if Defined(k,db) then ShowMsg(1)
      else db := Insert(k,d,db);

  GUI_LookUp : () ==> ()
  GUI_LookUp() ==
    let k = GetKey() in
      if Defined(k,db) then
         let d = LookUp(k,db) in
          ShowData(k,d)   	
      else 
        ShowMsg(3);

  GUI_Defined : () ==> ()
  GUI_Defined() ==
    let k = GetKey() in
      let found = Defined(k, db) in
        ShowDefined(k, found);

  GUI_Remove : () ==> ()
  GUI_Remove () == 
    let k = GetKey() in
      if Defined(k,db) then
         db := Remove(k, db)
      else ShowMsg(4);
  
  GUI_Menu : () ==> ()
  GUI_Menu() ==
  (
       GUI_Init();
       (dcl select : nat := 1;
       while (select > 0) do
        (
           select := SelectFunc();
           cases select :
             1 -> GUI_Insert(),
             2 -> GUI_Defined(),
             3 -> GUI_LookUp(),
             4 -> GUI_Remove(),
            others -> select := 0
          end
       )
     )
  )

end GuiDB
\end{vdm_al}
